<?xml version="1.0" encoding="ISO-8859-1" standalone="no" ?>
<!--
  ~ [y] hybris Platform
  ~ 
  ~ Copyright (c) 2000-2016 SAP SE
  ~ All rights reserved.
  ~ 
  ~ This software is the confidential and proprietary information of SAP 
  ~ Hybris ("Confidential Information"). You shall not disclose such 
  ~ Confidential Information and shall use it only in accordance with the 
  ~ terms of the license agreement you entered into with SAP Hybris.
  -->
<xsl:transform xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0" ><xsl:output method="text"/>

<xsl:param name="package"/>
<xsl:param name="packageRoot"/>

<!--<xsl:param name="extension_types_root_dir"/>-->

<xsl:variable name="lowerLetters" select="'abcdefghijklmnopqrstuvwxyz'"/>
<xsl:variable name="upperLetters" select="'ABCDEFGHIJKLMNOPQRSTUVWXYZ'"/>

<xsl:template match="items">


	<xsl:if test="contains($package,'${')">
	   <xsl:message terminate="yes"> Illegal package '<xsl:value-of select="$package"/>' !!! </xsl:message>
    </xsl:if>
    <xsl:if test="contains($packageRoot,'${')">
	   <xsl:message terminate="yes"> Illegal package root '<xsl:value-of select="$packageRoot"/>' !!! </xsl:message>
    </xsl:if>

package <xsl:value-of select="$package"/>;

import java.util.*;
import java.io.Serializable;
import de.hybris.platform.util.*;
import de.hybris.platform.core.*;
import de.hybris.platform.jalo.JaloBusinessException;
import de.hybris.platform.jalo.type.*;
import de.hybris.platform.persistence.type.*;
import de.hybris.platform.persistence.enumeration.*;
import de.hybris.platform.persistence.property.PersistenceManager;
import de.hybris.platform.persistence.*;

/**
 * Generated by hybris Platform.
 */
@SuppressWarnings({"cast","unused","boxing","null", "PMD"})
public class GeneratedTypeInitializer extends AbstractTypeInitializer
{
	/**
	 * Generated by hybris Platform.
	 */
	public GeneratedTypeInitializer( ManagerEJB manager, Map params )
	{
		super( manager, params );
	}


	/**
	 * Generated by hybris Platform.
	 */
	@Override
	protected void performRemoveObjects( ManagerEJB manager, Map params ) throws JaloBusinessException
	{
		// no-op by now
	}

	/**
	 * Generated by hybris Platform.
	 */
	@Override
	protected final void performCreateTypes( final ManagerEJB manager, Map params ) throws JaloBusinessException
	{
		// performCreateTypes
	<!-- create ComposedTypes -->
	<xsl:for-each select="itemtypes/itemtype[not(@autocreate) or @autocreate='true'] | itemtypes/typegroup/itemtype[not(@autocreate) or @autocreate='true']">
		<xsl:call-template name="checkQualifier">
			<xsl:with-param name="qualifiername" select="@code"/>
			<xsl:with-param name="string">itemtype code</xsl:with-param>
		</xsl:call-template>
		createItemType(
			"<xsl:value-of select="@code"/>",
			<xsl:choose>
				<!-- got some supertype ? -->
				<xsl:when test="@extends and not(@extends='')">"<xsl:value-of select="@extends"/>"</xsl:when>
				<!-- fallback to generic item for types created here except Item itself -->
				<xsl:when test="not( @code='Item' )">"GenericItem"</xsl:when>
				<!-- ok, no super type -->
				<xsl:otherwise>null</xsl:otherwise></xsl:choose>,
			<xsl:choose>
				<xsl:when test="@jaloclass and not(@jaloclass='')"><xsl:value-of select="@jaloclass"/>.class</xsl:when>
				<xsl:when test="not(@jaloclass) and (not(@generate) or @generate='true')"><xsl:value-of select="concat($packageRoot,'.jalo.', translate(substring(@code, 1,1),$lowerLetters,$upperLetters),substring(@code, 2))"/>.class</xsl:when>
				<xsl:otherwise>null</xsl:otherwise>
			</xsl:choose>,
			<xsl:choose>
				<xsl:when test="@deployment and not(@deployment='')">"<xsl:value-of select="@deployment"/>"</xsl:when>
				<xsl:when test="deployment and not(@deployment)">"<xsl:value-of select="concat('de.hybris.platform.persistence.',$package,'_',@code)"/>"</xsl:when>
				<xsl:otherwise>null</xsl:otherwise>
			</xsl:choose>,
			<xsl:choose><xsl:when test="@singleton='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
			<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
			<xsl:choose>
				<xsl:when test="@abstract='true'">true</xsl:when>
				<!--
					<xsl:choose>
						<xsl:when test="( @deployment and not(@deployment='') ) or ( deployment and not(@deployment) )">false</xsl:when>
						<xsl:otherwise>true</xsl:otherwise>
					</xsl:choose>
				</xsl:when>
				-->
				<xsl:otherwise>false</xsl:otherwise>
			</xsl:choose>
		);
	</xsl:for-each>

	<!-- create Relations -->
	<xsl:for-each select="relations/relation[not(@autocreate) or @autocreate='true']">
		<xsl:call-template name="checkQualifier">
			<xsl:with-param name="qualifiername" select="@code"/>
			<xsl:with-param name="string">relation code</xsl:with-param>
		</xsl:call-template>
		createRelationType(
			"<xsl:value-of select="@code"/>",
			<xsl:choose>
				<xsl:when test="@deployment and not(@deployment='')">"<xsl:value-of select="@deployment"/>"</xsl:when>
				<xsl:when test="deployment and not(@deployment)">"<xsl:value-of select="concat('de.hybris.platform.persistence.link.',$package,'_',@code)"/>"</xsl:when>
				<xsl:otherwise>null</xsl:otherwise>
			</xsl:choose>,
			<xsl:choose>
				<xsl:when test="sourceElement/@cardinality='one' or targetElement/@cardinality='one'">true</xsl:when>
				<xsl:otherwise>false</xsl:otherwise>
			</xsl:choose>
		);
	</xsl:for-each>

	<!-- create AtomicTypes -->
	<xsl:for-each select="atomictypes/atomictype[not(@autocreate) or @autocreate='true']">
		<xsl:call-template name="checkQualifier">
			<xsl:with-param name="qualifiername" select="@class"/>
			<xsl:with-param name="string">atomictype class</xsl:with-param>
		</xsl:call-template>
		createAtomicType(
			<xsl:choose>
				<xsl:when test="@extends and not(@extends='')"> <xsl:value-of select="@extends"/>.class</xsl:when>
				<xsl:otherwise>null</xsl:otherwise>
			</xsl:choose>,
			<xsl:value-of select="@class"/>.class
		);
	</xsl:for-each>

	<!-- create EnumerationTypes -->
	<xsl:for-each select="enumtypes/enumtype[not(@autocreate) or @autocreate='true']">
		<xsl:call-template name="checkQualifier">
			<xsl:with-param name="qualifiername" select="@code"/>
			<xsl:with-param name="string">enumtype code</xsl:with-param>
		</xsl:call-template>
		createEnumerationType(
			"<xsl:value-of select="@code"/>",
			<xsl:choose>
				<xsl:when test="@jaloclass and not(@jaloclass='')">
					<xsl:value-of select="@jaloclass"/>.class
				</xsl:when>
				<xsl:otherwise>null</xsl:otherwise>
			</xsl:choose>
		);
	</xsl:for-each>

	<!-- create CollectionTypes -->
	<xsl:for-each select="collectiontypes/collectiontype[not(@autocreate) or @autocreate='true']">
		<xsl:call-template name="checkQualifier">
			<xsl:with-param name="qualifiername" select="@code"/>
			<xsl:with-param name="string">collectiontype code</xsl:with-param>
		</xsl:call-template>
		createCollectionType(
			"<xsl:value-of select="@code"/>",
			"<xsl:value-of select="@elementtype"/>",
			<xsl:choose><xsl:when test="@type='set'">CollectionType.SET</xsl:when>
				<xsl:when test="@type='list'">CollectionType.LIST</xsl:when>
				<xsl:when test="@type='sortedset'">CollectionType.SORTED_SET</xsl:when>
				<xsl:otherwise>CollectionType.COLLECTION</xsl:otherwise>
			</xsl:choose>
		);
	</xsl:for-each>

	<!-- MapTypes -->
	<xsl:for-each select="maptypes/maptype[not(@autocreate) or @autocreate='true']">
		<xsl:call-template name="checkQualifier">
			<xsl:with-param name="qualifiername" select="@code"/>
			<xsl:with-param name="string">maptype code</xsl:with-param>
		</xsl:call-template>
		createMapType(
			"<xsl:value-of select="@code"/>",
			"<xsl:value-of select="@argumenttype"/>",
			"<xsl:value-of select="@returntype"/>"
		);
	</xsl:for-each>
	}

	/**
	 * Generated by hybris Platform.
	 */
	@Override
	protected final void performModifyTypes( final ManagerEJB manager, Map params ) throws JaloBusinessException
	{
		// performModifyTypes
	<!-- create item type attributes -->

	<!-- check redeclare definition -->
	<xsl:for-each select="itemtypes/itemtype/attributes/attribute[not(redeclare='false')]">
		<xsl:if test="@autocreate='false'">
			<xsl:message terminate="no">*** redeclaration of '<xsl:value-of select="../../@code"/>.<xsl:value-of select="@qualifier"/>' will be skipped because 'autocreate' is set to 'false'! (should be 'true') ***</xsl:message>
    	</xsl:if>
	</xsl:for-each>

	<xsl:for-each select="itemtypes/itemtype/attributes/attribute[not(@autocreate='false')] | itemtypes/typegroup/itemtype/attributes/attribute[not(@autocreate='false')]">
		<xsl:choose>
			<xsl:when test="not(@redeclare='true')">
				single_createattr_<xsl:value-of select="../../@code"/>_<xsl:value-of select="@qualifier"/>();
			</xsl:when>
		</xsl:choose>
	</xsl:for-each>

	<!-- create relation type attributes -->

	<xsl:for-each select="relations/relation[not(@autocreate) or @autocreate='true']">
		createRelationAttributes(
			"<xsl:value-of select="@code"/>", <!-- relation name -->
			<xsl:value-of select="@localized"/>, <!-- localized -->

			"<xsl:value-of select="sourceElement/@qualifier"/>", <!-- source qualifier -->
			"<xsl:value-of select="sourceElement/@type"/>", <!-- source type name -->
			<xsl:choose>
				<xsl:when test="sourceElement/@navigable='false'">false</xsl:when>
				<xsl:otherwise>true</xsl:otherwise>
			</xsl:choose>,
			<xsl:call-template name="getModifiers"><xsl:with-param name="node" select="sourceElement"/></xsl:call-template>, <!-- source modifiers -->
			<xsl:choose>
				<xsl:when test="sourceElement/@cardinality='one'">false</xsl:when>
				<xsl:otherwise>true</xsl:otherwise>
			</xsl:choose>,
			<xsl:choose>
				<xsl:when test="sourceElement/@ordered='true'">true</xsl:when>
				<xsl:when test="sourceElement/@ordered='false'">false</xsl:when>
				<xsl:when test="sourceElement/@cardinality='one' or targetElement/@cardinality='one'">false</xsl:when>
				<xsl:when test="sourceElement/@collectiontype='list'">true</xsl:when>
				<!-- n-m are considered not-ordered as default -->
				<xsl:otherwise>false</xsl:otherwise>
			</xsl:choose>,
			<xsl:choose>
				<xsl:when test="sourceElement/@collectiontype='set'">CollectionType.SET</xsl:when>
				<xsl:when test="sourceElement/@collectiontype='list'">CollectionType.LIST</xsl:when>
				<xsl:when test="sourceElement/@collectiontype='sortedset'">CollectionType.SORTED_SET</xsl:when>
				<xsl:otherwise>CollectionType.COLLECTION</xsl:otherwise>
			</xsl:choose>,
			"<xsl:value-of select="targetElement/@qualifier"/>", <!-- target qualifier -->
			"<xsl:value-of select="targetElement/@type"/>", <!-- target type name -->
			<xsl:choose>
				<xsl:when test="targetElement/@navigable='false'">false</xsl:when>
				<xsl:otherwise>true</xsl:otherwise>
			</xsl:choose>,
			<xsl:call-template name="getModifiers"><xsl:with-param name="node" select="targetElement"/></xsl:call-template>, <!-- target modifiers -->
			<xsl:choose>
				<xsl:when test="targetElement/@cardinality='one'">false</xsl:when>
				<xsl:otherwise>true</xsl:otherwise>
			</xsl:choose>,
			<xsl:choose>
				<xsl:when test="targetElement/@ordered='true'">true</xsl:when>
				<xsl:when test="targetElement/@ordered='false'">false</xsl:when>
				<xsl:when test="sourceElement/@cardinality='one' or targetElement/@cardinality='one'">false</xsl:when>
				<xsl:when test="targetElement/@collectiontype='list'">true</xsl:when>
				<!-- n-m are considered not-ordered as default -->
				<xsl:otherwise>false</xsl:otherwise>
			</xsl:choose>,
			<xsl:choose>
				<xsl:when test="targetElement/@collectiontype='set'">CollectionType.SET</xsl:when>
				<xsl:when test="targetElement/@collectiontype='list'">CollectionType.LIST</xsl:when>
				<xsl:when test="targetElement/@collectiontype='sortedset'">CollectionType.SORTED_SET</xsl:when>
				<xsl:otherwise>CollectionType.COLLECTION</xsl:otherwise>
			</xsl:choose>
		);
	</xsl:for-each>

	<xsl:for-each select="itemtypes/itemtype/attributes/attribute[not(@autocreate='false')] | itemtypes/typegroup/itemtype/attributes/attribute[not(@autocreate='false')]">
		<xsl:choose>
			<xsl:when test="@redeclare='true'">
				single_createattr_<xsl:value-of select="../../@code"/>_<xsl:value-of select="@qualifier"/>();
			</xsl:when>
		</xsl:choose>
	</xsl:for-each>

	}

	<xsl:for-each select="itemtypes/itemtype/attributes/attribute[not(@autocreate='false')] | itemtypes/typegroup/itemtype/attributes/attribute[not(@autocreate='false')]">
	public void single_createattr_<xsl:value-of select="../../@code"/>_<xsl:value-of select="@qualifier"/>() throws JaloBusinessException
	{
		<xsl:choose>
			<xsl:when test="@redeclare='true'">
				redeclareAttribute(
					"<xsl:value-of select="../../@code"/>", <!-- itemtype code -->
					"<xsl:value-of select="@qualifier"/>",
					"<xsl:choose><!-- public qualifier -->
						<xsl:when test="@type='boolean'">java.lang.Boolean</xsl:when>
						<xsl:when test="@type='int'">java.lang.Integer</xsl:when>
						<xsl:when test="@type='long'">java.lang.Long</xsl:when>
						<xsl:when test="@type='float'">java.lang.Float</xsl:when>
						<xsl:when test="@type='double'">java.lang.Double</xsl:when>
						<xsl:when test="@type='char'">java.lang.Character</xsl:when>
						<xsl:when test="@type='short'">java.lang.Short</xsl:when>
						<xsl:when test="@type='byte'">java.lang.Byte</xsl:when>
						<xsl:otherwise><xsl:value-of select="@type"/></xsl:otherwise><!-- attribute type name -->
					</xsl:choose>",
					<xsl:call-template name="getModifiers"/>
				);
			</xsl:when>
			<xsl:when test="not(@redeclare='true') and persistence/@type='cmp'">
				//createCMPAttribute( String itemTypeName, String qualifier, String attributeTypeName, String persistenceQualifier, String metaTypeName, int modifiers )
				createCMPAttribute(
					"<xsl:value-of select="../../@code"/>", <!-- itemtype code -->
					"<xsl:value-of select="@qualifier"/>",  <!-- public qualifier -->
					"<xsl:value-of select="@type"/>", <!-- attribute type name -->
					"<xsl:value-of select="persistence/@qualifier"/>", <!-- persistence qualifier -->
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
					<xsl:call-template name="getModifiers"/>
				);
			</xsl:when>
			<xsl:when test="not(@redeclare='true') and persistence/@type='jalo'">
				createJaloAttribute(
					"<xsl:value-of select="../../@code"/>", <!-- itemtype code -->
					"<xsl:value-of select="@qualifier"/>",  <!-- public qualifier -->
					"<xsl:choose><!-- public qualifier -->
						<xsl:when test="@type='boolean'">java.lang.Boolean</xsl:when>
						<xsl:when test="@type='int'">java.lang.Integer</xsl:when>
						<xsl:when test="@type='long'">java.lang.Long</xsl:when>
						<xsl:when test="@type='float'">java.lang.Float</xsl:when>
						<xsl:when test="@type='double'">java.lang.Double</xsl:when>
						<xsl:when test="@type='char'">java.lang.Character</xsl:when>
						<xsl:when test="@type='short'">java.lang.Short</xsl:when>
						<xsl:when test="@type='byte'">java.lang.Byte</xsl:when>
						<xsl:otherwise><xsl:value-of select="@type"/></xsl:otherwise>
					</xsl:choose>",
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
					<xsl:call-template name="getModifiers"/>
				);
			</xsl:when>
			<xsl:when test="not(@redeclare='true') and persistence/@type='dynamic'">
				createDynamicAttribute(
					"<xsl:value-of select="../../@code"/>", <!-- itemtype code -->
					"<xsl:value-of select="@qualifier"/>",  <!-- public qualifier -->
					"<xsl:choose><!-- public qualifier -->
						<xsl:when test="@type='boolean'">java.lang.Boolean</xsl:when>
						<xsl:when test="@type='int'">java.lang.Integer</xsl:when>
						<xsl:when test="@type='long'">java.lang.Long</xsl:when>
						<xsl:when test="@type='float'">java.lang.Float</xsl:when>
						<xsl:when test="@type='double'">java.lang.Double</xsl:when>
						<xsl:when test="@type='char'">java.lang.Character</xsl:when>
						<xsl:when test="@type='short'">java.lang.Short</xsl:when>
						<xsl:when test="@type='byte'">java.lang.Byte</xsl:when>
						<xsl:otherwise><xsl:value-of select="@type"/></xsl:otherwise>
					</xsl:choose>",
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
					<xsl:call-template name="getModifiers"/>
				);
			</xsl:when>			
			<xsl:when test="not(@redeclare='true') and persistence/@type='property'">
				<xsl:choose>
					<xsl:when test="persistence/columntype">
						Map sqlColumnDefinitions = new HashMap();
						<xsl:for-each select="persistence/columntype">
							sqlColumnDefinitions.put(
								<xsl:choose>
									<xsl:when test="@database and not(@database='')">"<xsl:value-of select="@database"/>"</xsl:when>
									<xsl:otherwise>de.hybris.platform.persistence.property.PersistenceManager.NO_DATABASE</xsl:otherwise>
								</xsl:choose>,
								"<xsl:value-of select="value"/>"
							);
						</xsl:for-each>
					</xsl:when>
					<xsl:otherwise>
						Map sqlColumnDefinitions = null;
					</xsl:otherwise>
				</xsl:choose>
				createPropertyAttribute(
					"<xsl:value-of select="../../@code"/>", <!-- itemtype code -->
					"<xsl:value-of select="@qualifier"/>",  <!-- public qualifier -->
					<xsl:choose> <!-- persistence qualifier -->
						<xsl:when test="persistence/@qualifier and not(persistence/@qualifier='')">"<xsl:value-of select="persistence/@qualifier"/>"</xsl:when>
						<xsl:otherwise>null</xsl:otherwise>
					</xsl:choose>,
					"<xsl:choose><!-- public qualifier -->
						<xsl:when test="@type='boolean'">java.lang.Boolean</xsl:when>
						<xsl:when test="@type='int'">java.lang.Integer</xsl:when>
						<xsl:when test="@type='long'">java.lang.Long</xsl:when>
						<xsl:when test="@type='float'">java.lang.Float</xsl:when>
						<xsl:when test="@type='double'">java.lang.Double</xsl:when>
						<xsl:when test="@type='char'">java.lang.Character</xsl:when>
						<xsl:when test="@type='short'">java.lang.Short</xsl:when>
						<xsl:when test="@type='byte'">java.lang.Byte</xsl:when>
						<xsl:otherwise><xsl:value-of select="@type"/></xsl:otherwise>
					</xsl:choose>",
					<xsl:call-template name="getModifiers"/>,
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
					sqlColumnDefinitions
				);
			</xsl:when>
			<xsl:when test="not(@redeclare='true') and not(persistence)">
				createJaloAttribute(
					"<xsl:value-of select="../../@code"/>", <!-- itemtype code -->
					"<xsl:value-of select="@qualifier"/>",  <!-- public qualifier -->
					"<xsl:choose><!-- public qualifier -->
						<xsl:when test="@type='boolean'">java.lang.Boolean</xsl:when>
						<xsl:when test="@type='int'">java.lang.Integer</xsl:when>
						<xsl:when test="@type='long'">java.lang.Long</xsl:when>
						<xsl:when test="@type='float'">java.lang.Float</xsl:when>
						<xsl:when test="@type='double'">java.lang.Double</xsl:when>
						<xsl:when test="@type='char'">java.lang.Character</xsl:when>
						<xsl:when test="@type='short'">java.lang.Short</xsl:when>
						<xsl:when test="@type='byte'">java.lang.Byte</xsl:when>
						<xsl:otherwise><xsl:value-of select="@type"/></xsl:otherwise>
					</xsl:choose>",
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
					<xsl:call-template name="getModifiers"/>
				);
			</xsl:when>
			<xsl:otherwise>
				<xsl:message terminate="yes">cannot process attribute <xsl:value-of select="../../@code"/>.<xsl:value-of select="@qualifier"/></xsl:message>
			</xsl:otherwise>
		</xsl:choose>
	}
	</xsl:for-each>


	/**
	 * Generated by hybris Platform.
	 */
	@Override
	protected final void performCreateObjects( final ManagerEJB manager, Map params ) throws JaloBusinessException
	{
		// performCreateObjects
	<!-- create enumeration values -->
	<xsl:for-each select="enumtypes/enumtype">
		createEnumerationValues(
			"<xsl:value-of select="@code"/>",
			<xsl:choose>
				<xsl:when test="@dynamic and not(@dynamic='')">
					<xsl:value-of select="@dynamic"/>
				</xsl:when>
				<xsl:otherwise>false</xsl:otherwise>
			</xsl:choose>,
			Arrays.asList( new String[] {
			<xsl:for-each select="value">
				"<xsl:value-of select="@code"/>"<xsl:if test="not(position()=last())">,</xsl:if>
			</xsl:for-each>
			} )
		);
	</xsl:for-each>

	<xsl:for-each select="relations/relation/sourceElement">
		single_setRelAttributeProperties_<xsl:value-of select="../@code"/>_source();
	</xsl:for-each>
	<xsl:for-each select="relations/relation/targetElement">
		single_setRelAttributeProperties_<xsl:value-of select="../@code"/>_target();
	</xsl:for-each>

	<!-- connect relation types -->
	<xsl:for-each select="relations/relation[not(@autocreate) or @autocreate='true']">
		connectRelation(
			"<xsl:value-of select="@code"/>", <!-- relation name -->
			<xsl:value-of select="@localized"/>, <!-- localized flag -->
			"<xsl:value-of select="sourceElement/@qualifier"/>", <!-- source qualifier -->
			"<xsl:value-of select="sourceElement/@type"/>", <!-- source type name -->
			<xsl:choose><xsl:when test="sourceElement/@navigable='false'">false</xsl:when><xsl:otherwise>true</xsl:otherwise></xsl:choose>,
			<xsl:call-template name="getModifiers"><xsl:with-param name="node" select="sourceElement"/></xsl:call-template>, <!-- source modifiers -->
			"<xsl:value-of select="targetElement/@qualifier"/>", <!-- target qualifier -->
			"<xsl:value-of select="targetElement/@type"/>", <!-- target type name -->
			<xsl:choose><xsl:when test="targetElement/@navigable='false'">false</xsl:when><xsl:otherwise>true</xsl:otherwise></xsl:choose>,
			<xsl:call-template name="getModifiers"><xsl:with-param name="node" select="targetElement"/></xsl:call-template>, <!-- target modifiers -->
			<xsl:choose><xsl:when test="not(@autocreate='false')">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
			<xsl:choose><xsl:when test="not(@generate) or @generate='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>
		);
	</xsl:for-each>

	<!-- set item type properties -->

	<xsl:for-each select="itemtypes/itemtype | itemtypes/typegroup/itemtype">
		<!-- set properties only for self-created itemtypes -->
		<xsl:choose>
			<xsl:when test="not(@autocreate) or @autocreate='true'">
				{
				Map customPropsMap = new HashMap();
				<xsl:for-each select="custom-properties/property">
					customPropsMap.put( "<xsl:value-of select="@name"/>", <xsl:value-of select="value"/> );
				</xsl:for-each>
				setItemTypeProperties(
					"<xsl:value-of select="@code"/>",
					<xsl:choose><xsl:when test="@jaloonly and @jaloonly='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
					<xsl:choose><xsl:when test="not(@autocreate='false')">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
					<xsl:choose><xsl:when test="not(@generate) or @generate='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
					customPropsMap
				);
				}
			</xsl:when>
			<xsl:otherwise>
				{
				Map customPropsMap = new HashMap();
				<xsl:for-each select="custom-properties/property">
					customPropsMap.put( "<xsl:value-of select="@name"/>", <xsl:value-of select="value"/> );
				</xsl:for-each>
				changeMetaType(
					"<xsl:value-of select="@code"/>",
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
					customPropsMap
				);
				}
			</xsl:otherwise>
		</xsl:choose>
		<!-- set attribute props for all attributes -->



		<xsl:for-each select="attributes/attribute">
			single_setAttributeProperties_<xsl:value-of select="../../@code"/>_<xsl:value-of select="@qualifier"/>();
		</xsl:for-each>

		<!-- indexes since PLA-10838 moved to dynamic creation -->
	</xsl:for-each>

	<!-- set collection type extension names -->
	<xsl:for-each select="collectiontypes/collectiontype">
		<xsl:choose>
			<xsl:when test="not(@autocreate) or @autocreate='true'">
				setDefaultProperties(
					"<xsl:value-of select="@code"/>",
					<xsl:choose><xsl:when test="not(@autocreate='false')">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
					<xsl:choose><xsl:when test="not(@generate) or @generate='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>
				);
			</xsl:when>
			<xsl:otherwise>
				changeMetaType(
					"<xsl:value-of select="@code"/>",
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
					null
				);
			</xsl:otherwise>
		</xsl:choose>
	</xsl:for-each>

	<!-- set map type extension names -->
	<xsl:for-each select="maptypes/maptype">
		<xsl:choose>
			<xsl:when test="not(@autocreate) or @autocreate='true'">
				setDefaultProperties(
					"<xsl:value-of select="@code"/>",
					<xsl:choose><xsl:when test="not(@autocreate='false')">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
					<xsl:choose><xsl:when test="not(@generate) or @generate='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>
				);
			</xsl:when>
			<xsl:otherwise>
				changeMetaType(
					"<xsl:value-of select="@code"/>",
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
					null
				);
			</xsl:otherwise>
		</xsl:choose>
	</xsl:for-each>

	<!-- set atomic type extension names -->
	<xsl:for-each select="atomictypes/atomictype">
		<xsl:choose>
			<xsl:when test="not(@autocreate) or @autocreate='true'">
				setDefaultProperties(
					<xsl:value-of select="@class"/>.class.getName(),
					<xsl:choose><xsl:when test="not(@autocreate='false')">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
					<xsl:choose><xsl:when test="not(@generate) or @generate='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>
				);
			</xsl:when>
			<xsl:otherwise>
				changeMetaType(
					<xsl:value-of select="@class"/>.class.getName(),
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
					null
				);
			</xsl:otherwise>
		</xsl:choose>
	</xsl:for-each>

	<!-- set enumeration type extension names -->
	<xsl:for-each select="enumtypes/enumtype">
		<xsl:choose>
			<xsl:when test="not(@autocreate) or @autocreate='true'">
				setDefaultProperties(
					"<xsl:value-of select="@code"/>",
					<xsl:choose><xsl:when test="not(@autocreate='false')">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
					<xsl:choose><xsl:when test="not(@generate) or @generate='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>
				);
			</xsl:when>
			<xsl:otherwise>
				changeMetaType(
					"<xsl:value-of select="@code"/>",
					<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
					null
				);
			</xsl:otherwise>
		</xsl:choose>
	</xsl:for-each>
	}


		<xsl:for-each select="itemtypes/itemtype | itemtypes/typegroup/itemtype">
			<xsl:for-each select="attributes/attribute">
				<xsl:choose>
					<xsl:when test="not(@autocreate='false')">
						public void single_setAttributeProperties_<xsl:value-of select="../../@code"/>_<xsl:value-of select="@qualifier"/>() throws JaloBusinessException
						{
							<!-- assemble default value defintion string -->
							<xsl:variable name="defstr">
								<xsl:call-template name="string-replace">
									<xsl:with-param name="string" select='defaultvalue'/>
									<xsl:with-param name="search-for"><xsl:value-of select="'&quot;'"/></xsl:with-param>
									<xsl:with-param name="replace-with"><xsl:value-of select="'\&quot;'"/></xsl:with-param>
								</xsl:call-template>
							</xsl:variable>
							<xsl:variable name="defstr2" select="normalize-space($defstr)"/>
							<!-- custom props -->
							Map customPropsMap = new HashMap();
							<xsl:for-each select="custom-properties/property">
								customPropsMap.put( "<xsl:value-of select="@name"/>", <xsl:value-of select="value"/> );
							</xsl:for-each>
							setAttributeProperties(
								"<xsl:value-of select="../../@code"/>", <!-- item type name -->
								"<xsl:value-of select="@qualifier"/>",<!-- qualifier -->
								<xsl:choose><xsl:when test="modifiers/@unique='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>, <!-- unique -->
								<xsl:choose><xsl:when test="defaultvalue"><xsl:value-of select="defaultvalue"/></xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
								<xsl:choose><xsl:when test="$defstr2 and not($defstr2='')">"<xsl:value-of select="$defstr2"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
								<xsl:choose><xsl:when test="@isSelectionOf and not(@isSelectionOf='')">"<xsl:value-of select="@isSelectionOf"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
								<xsl:choose><xsl:when test="not(@autocreate='false')">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
								<xsl:choose><xsl:when test="not(@generate) or @generate='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
								<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
								customPropsMap,
								<xsl:choose><xsl:when test="persistence/@type='dynamic'"><xsl:choose><xsl:when test="persistence/@attributeHandler">"<xsl:value-of select="persistence/@attributeHandler"/>"</xsl:when><xsl:otherwise>"<xsl:value-of select="../../@code" />_<xsl:value-of select="@qualifier" />AttributeHandler"</xsl:otherwise></xsl:choose></xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>
							);
						}
					</xsl:when>
					<xsl:otherwise>
						public void single_setAttributeProperties_<xsl:value-of select="../../@code"/>_<xsl:value-of select="@qualifier"/>() throws JaloBusinessException
						{
							Map customPropsMap = new HashMap();
							<xsl:for-each select="custom-properties/property">
								customPropsMap.put( "<xsl:value-of select="@name"/>", <xsl:value-of select="value"/> );
							</xsl:for-each>
							changeAttributeMetaType(
								"<xsl:value-of select="../../@code"/>", <!-- item type name -->
								"<xsl:value-of select="@qualifier"/>",<!-- qualifier -->
								<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
								customPropsMap
							);
						}
					</xsl:otherwise>
				</xsl:choose>
			</xsl:for-each>
		</xsl:for-each>


		<xsl:for-each select="relations/relation">
			<xsl:for-each select="sourceElement">
				<xsl:choose>
					<xsl:when test="not(@navigable='false')">
						<xsl:choose>
							<xsl:when test="not(../@autocreate='false')">
								public void single_setRelAttributeProperties_<xsl:value-of select="../@code"/>_source() throws JaloBusinessException
								{
									<!-- custom props -->
									Map customPropsMap = new HashMap();
									<xsl:for-each select="custom-properties/property">
										customPropsMap.put( "<xsl:value-of select="@name"/>", <xsl:value-of select="value"/> );
									</xsl:for-each>

									setAttributeProperties(
										"<xsl:value-of select="../targetElement/@type"/>", <!-- item type name -->
										"<xsl:value-of select="@qualifier"/>",<!-- qualifier -->
										<xsl:choose><xsl:when test="modifiers/@unique='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>, <!-- unique -->
										null,
										null,
										null,
										<xsl:choose><xsl:when test="not(../@autocreate='false')">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
										<xsl:choose><xsl:when test="not(../@generate) or ../@generate='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
										<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
										customPropsMap,
										<xsl:choose><xsl:when test="persistence/@type='dynamic'">"<xsl:value-of select="persistence/@attributeHandler"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>
									);
								}
							</xsl:when>
							<xsl:otherwise>
								public void single_setRelAttributeProperties_<xsl:value-of select="../@code"/>_source() throws JaloBusinessException
								{
									Map customPropsMap = new HashMap();
									<xsl:for-each select="custom-properties/property">
										customPropsMap.put( "<xsl:value-of select="@name"/>", <xsl:value-of select="value"/> );
									</xsl:for-each>
									changeAttributeMetaType(
										"<xsl:value-of select="../targetElement/@type"/>", <!-- item type name -->
										"<xsl:value-of select="@qualifier"/>",<!-- qualifier -->
										<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
										customPropsMap
									);
								}
							</xsl:otherwise>
						</xsl:choose>
					</xsl:when>
					<xsl:otherwise>
							public void single_setRelAttributeProperties_<xsl:value-of select="../@code"/>_source() throws JaloBusinessException
							{
								//nothing to do
							}
					</xsl:otherwise>
				</xsl:choose>
			</xsl:for-each>
			<xsl:for-each select="targetElement">
				<xsl:choose>
					<xsl:when test="not(@navigable='false')">
						<xsl:choose>
							<xsl:when test="not(../@autocreate='false')">
								public void single_setRelAttributeProperties_<xsl:value-of select="../@code"/>_target() throws JaloBusinessException
								{
									<!-- custom props -->
									Map customPropsMap = new HashMap();
									<xsl:for-each select="custom-properties/property">
										customPropsMap.put( "<xsl:value-of select="@name"/>", <xsl:value-of select="value"/> );
									</xsl:for-each>

									setAttributeProperties(
										"<xsl:value-of select="../sourceElement/@type"/>", <!-- item type name -->
										"<xsl:value-of select="@qualifier"/>",<!-- qualifier -->
										<xsl:choose><xsl:when test="modifiers/@unique='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>, <!-- unique -->
										null,
										null,
										null,
										<xsl:choose><xsl:when test="not(../@autocreate='false')">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
										<xsl:choose><xsl:when test="not(../@generate) or ../@generate='true'">true</xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>,
										<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
										customPropsMap,
										<xsl:choose><xsl:when test="persistence/@type='dynamic'">"<xsl:value-of select="persistence/@attributeHandler"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>
									);
								}
							</xsl:when>
							<xsl:otherwise>
								public void single_setRelAttributeProperties_<xsl:value-of select="../@code"/>_target() throws JaloBusinessException
								{
									Map customPropsMap = new HashMap();
									<xsl:for-each select="custom-properties/property">
										customPropsMap.put( "<xsl:value-of select="@name"/>", <xsl:value-of select="value"/> );
									</xsl:for-each>
									changeAttributeMetaType(
										"<xsl:value-of select="../sourceElement/@type"/>", <!-- item type name -->
										"<xsl:value-of select="@qualifier"/>",<!-- qualifier -->
										<xsl:choose><xsl:when test="@metatype and not(@metatype='')">"<xsl:value-of select="@metatype"/>"</xsl:when><xsl:otherwise>null</xsl:otherwise></xsl:choose>,
										customPropsMap
									);
								}
							</xsl:otherwise>
						</xsl:choose>
					</xsl:when>
					<xsl:otherwise>
							public void single_setRelAttributeProperties_<xsl:value-of select="../@code"/>_target() throws JaloBusinessException
							{
								//nothing to do
							}
					</xsl:otherwise>
				</xsl:choose>
			</xsl:for-each>
		</xsl:for-each>
}

	</xsl:template>


	<!--
	  ** returns a text containing the modifier expression needed to create or redeclare
	  ** a attribute. the following defaults are used:
	  ** read 		= 'true'
	  ** write		= 'true'
	  ** optional 	= 'true'
	  ** initial 	= 'false'
	  ** private 	= 'false'
	  ** search 	= 'true'
	  ** encrypted 	= 'false'
	  -->
	<xsl:template name="getModifiers">
		<xsl:param name="node" select="."/>
		<xsl:for-each select="$node">
			<xsl:variable name="res">
			   <!-- READ -->
			   <xsl:choose>
			      <xsl:when test="modifiers/@read and modifiers/@read='true'">de.hybris.platform.jalo.type.AttributeDescriptor.READ_FLAG|</xsl:when>
			      <xsl:when test="not(modifiers/@read)">de.hybris.platform.jalo.type.AttributeDescriptor.READ_FLAG|</xsl:when>
			   </xsl:choose>
			   <!-- WRITE -->
			   <xsl:choose>
			      <xsl:when test="modifiers/@write and modifiers/@write='true'">de.hybris.platform.jalo.type.AttributeDescriptor.WRITE_FLAG|</xsl:when>
			      <xsl:when test="not(modifiers/@write)">de.hybris.platform.jalo.type.AttributeDescriptor.WRITE_FLAG|</xsl:when>
			   </xsl:choose>
			   <!-- OPTIONAL -->
			   <xsl:choose>
			      <xsl:when test="modifiers/@optional and modifiers/@optional='true'">de.hybris.platform.jalo.type.AttributeDescriptor.OPTIONAL_FLAG|</xsl:when>
			      <xsl:when test="not(modifiers/@optional)">de.hybris.platform.jalo.type.AttributeDescriptor.OPTIONAL_FLAG|</xsl:when>
			   </xsl:choose>
			   <!-- INITIAL -->
			   <xsl:choose>
			      <xsl:when test="modifiers/@initial and modifiers/@initial='true'">de.hybris.platform.jalo.type.AttributeDescriptor.INITIAL_FLAG|</xsl:when>
			   </xsl:choose>
			   <!-- PRIVATE -->
			   <xsl:choose>
			      <xsl:when test="modifiers/@private and modifiers/@private='true'">de.hybris.platform.jalo.type.AttributeDescriptor.PRIVATE_FLAG|</xsl:when>
			   </xsl:choose>
			   <!-- PARTOF -->
			   <xsl:choose>
			      <xsl:when test="modifiers/@partof and modifiers/@partof='true'">de.hybris.platform.jalo.type.AttributeDescriptor.PARTOF_FLAG|</xsl:when>
			   </xsl:choose>
			   <!-- REMOVABLE -->
			   <xsl:choose>
			      <xsl:when test="modifiers/@removable and modifiers/@removable='true'">de.hybris.platform.jalo.type.AttributeDescriptor.REMOVE_FLAG|</xsl:when>
			      <xsl:when test="not(modifiers/@removable)">de.hybris.platform.jalo.type.AttributeDescriptor.REMOVE_FLAG|</xsl:when>
			   </xsl:choose>
			   <!-- SEARCH -->
			   <xsl:choose>
			      <xsl:when test="modifiers/@search and modifiers/@search='true'">de.hybris.platform.jalo.type.AttributeDescriptor.SEARCH_FLAG|</xsl:when>
			      <xsl:when test="not(modifiers/@search)">de.hybris.platform.jalo.type.AttributeDescriptor.SEARCH_FLAG|</xsl:when>
			   </xsl:choose>
			   <!-- DONTOPTIMIZE -->
			   <xsl:choose>
			      <xsl:when test="modifiers/@dontOptimize and modifiers/@dontOptimize='true'">de.hybris.platform.jalo.type.AttributeDescriptor.TIM_IGNORE_FLAG|</xsl:when>
			   </xsl:choose>
			   <!-- ENCRYPTED -->
			   <xsl:choose>
			      <xsl:when test="modifiers/@encrypted and modifiers/@encrypted='true'">de.hybris.platform.jalo.type.AttributeDescriptor.ENCRYPTED_FLAG|</xsl:when>
			   </xsl:choose>
			   <!-- PRIMITIVE -->
			   <xsl:choose>
			      <xsl:when test="@type='boolean' or @type='int' or @type='long' or @type='double' or @type='float' or @type='short' or @type='byte' or @type='char'">de.hybris.platform.jalo.type.AttributeDescriptor.PRIMITIVE_FLAG|</xsl:when>
			   </xsl:choose>
			</xsl:variable>
			<!-- output (cut last '|')-->
			<xsl:choose>
				<xsl:when test="string-length($res)&gt;0"><xsl:value-of select="substring($res,1,string-length($res)-1)"/></xsl:when>
				<xsl:otherwise>0</xsl:otherwise>
			</xsl:choose>
		</xsl:for-each>
	</xsl:template>

	<!-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	  ~~~ helper templates                                                 ~~~
	  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-->

	<xsl:template name="string-replace">
		<xsl:param name="string" />
		<xsl:param name="search-for" />
		<xsl:param name="replace-with" />
		<xsl:choose>
			<xsl:when test='contains($string,$search-for)'>
			<xsl:value-of select="substring-before($string,$search-for)"/>
			<xsl:value-of select="$replace-with"/>
				<xsl:call-template name="string-replace">
					<xsl:with-param
						name="string"
						select="substring-after($string,$search-for)" />
					<xsl:with-param name="search-for" select="$search-for" />
					<xsl:with-param name="replace-with" select="$replace-with" />
				</xsl:call-template>
			</xsl:when>
			<xsl:otherwise>
				<xsl:value-of select="$string" />
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

	<xsl:template name="checkQualifier">
		<xsl:param name="qualifiername" />
		<xsl:param name="string" />

		<xsl:if test="not($qualifiername)"><xsl:message terminate="yes"> missing <xsl:value-of select="$string"/> </xsl:message></xsl:if>
		<xsl:if test="contains( translate($qualifiername, ' ,;������', '$$$$$$$$$'), '$') ">
			<xsl:message terminate="yes"><xsl:value-of select="$string"/> <xsl:value-of select="$qualifiername"/> contains illegal characters in <xsl:value-of select="."/></xsl:message>
		</xsl:if>

	</xsl:template>

</xsl:transform>

